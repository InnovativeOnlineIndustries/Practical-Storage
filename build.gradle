plugins {
    id 'java'
    id "fabric-loom" version "1.0-SNAPSHOT"
    id "io.github.juuxel.loom-quiltflower" version "1.+"
}

group = 'com.buuz135'
version = '1.19.2-1.1.2'

java {
    archivesBaseName = 'functionalstorage'
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

loom {
    accessWidenerPath = file("src/main/resources/functional.accesswidener")

    runs {
        datagen {
            client()

            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
            vmArg "-Dfabric-api.datagen.modid=functionalstorage"

            property "com.buuz135.functionalstorage.data.existingData", file("src/main/resources").absolutePath

            runDir "build/datagen"
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenLocal()
    maven {
        name 'jared maven'
        url "https://maven.blamejared.com/"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url = "https://www.cursemaven.com"
    }
    maven {
        url = "https://maven.wispforest.io"
        content {
            includeGroup("mcjty.theoneprobe")
        }
    }
    maven { url "https://maven.shedaniel.me/" }
    maven { url = 'https://ladysnake.jfrog.io/artifactory/mods' }
    maven { url = "https://mvn.devos.one/snapshots/" }
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" }
    maven { url = "https://jitpack.io" }
    maven { url = "https://api.modrinth.com/maven" }
}

dependencies {
    minecraft "com.mojang:minecraft:${mc_version}"
    mappings loom.officialMojangMappings()

    modImplementation("net.fabricmc:fabric-loader:${loader_version}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${fabric_version}")
    modImplementation(include("com.hrznstudio:titanium:1.19.2-3.7.1"))
    modImplementation(include("io.github.fabricators_of_create.Porting-Lib:porting-lib:${project.port_lib_version}+${project.mc_version}"))
    modImplementation(include("io.github.tropheusj:milk-lib:${project.milk_lib_version}"))
    modCompileOnly("mezz.jei:jei-${mc_version}-common-api:${jei_version}")
    modCompileOnly("mezz.jei:jei-${mc_version}-fabric-api:${jei_version}")
    modRuntimeOnly("mezz.jei:jei-${mc_version}-fabric:${jei_version}")
    //runtimeOnly fg.deobf("curse.maven:refined-storage-243076:3712882")
    modImplementation("mcjty.theoneprobe:theoneprobe-fabric:${top_version}")
    //implementation fg.deobf("curse.maven:allthecompressed-514045:3723881")
//    modRuntimeOnly("curse.maven:create-fabric-624165:4125637")
    //runtimeOnly fg.deobf("curse.maven:flywheel-486392:3600401")
    //implementation fg.deobf("curse.maven:mekanism-268560:3810540")
    //runtimeOnly fg.deobf("curse.maven:refined-pipes-370696:3816785")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : "functionalstorage",
                "Specification-Vendor"    : "Buuz135, Rid",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Buuz135, Rid",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
